Import("coreenv")


coreenv.Append(
    BUILDERS={
        "HEXBuilder": Builder(
            action=Action(
                '${OBJCOPY} -O ihex "${SOURCE}" "${TARGET}"',
                "\tHEX\t${TARGET}",
            ),
            suffix=".hex",
            src_suffix=".elf",
        ),
        "BINBuilder": Builder(
            action=Action(
                '${OBJCOPY} -O binary -S "${SOURCE}" "${TARGET}"',
                "\tBIN\t${TARGET}",
            ),
            suffix=".bin",
            src_suffix=".elf",
        ),
        "DFUBuilder": Builder(
            action=Action(
                '${PYTHON3} ./scripts/bin2dfu.py -i "${SOURCE}" -o "${TARGET}" -a ${IMAGE_BASE_ADDRESS} -l "Flipper Zero F${TARGET_HW}"',
                "\tDFU\t${TARGET}",
            ),
            suffix=".dfu",
            src_suffix=".bin",
        ),
        "ELFStripper": Builder(
            action=Action(
                coreenv["STRIPCOM"],
                "\tSTRIP\t${TARGET}",
            ),
            suffix=".elf",
            src_suffix=".elf",
        ),
        "Flasher": Builder(
            action=[
                Action(
                    'openocd ${OPENOCD_OPTS} -c "program ${SOURCE.posix} reset exit ${IMAGE_BASE_ADDRESS}"',
                    "\tFLASH\t${SOURCE}",
                ),
                Touch("${TARGET}"),
            ],
            suffix=".flash",
            src_suffix=".bin",
        ),
    }
)
